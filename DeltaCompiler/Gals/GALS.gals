#Options
GenerateScanner = true
GenerateParser = true
Language = C++
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Full
Input = Stream
Parser = SLR
#RegularDefinitions
LS : [^\"]|(\\\")
LC : [^']|(\\')
D : [0-9]

DEC : [0-9]+
HEX : 0[xX][0-9a-fA-F]+
BIN : 0[bB][01]+

CMT: (/\*([^\*]|[\r\n]|(\*+([^\*/]|[\r\n])))*\*+/)|([/][/].*)
#Tokens
INT_DEC : {DEC}
INT_HEX : {HEX}
INT_BIN : {BIN}
FLOAT : {D}+(\.{D}+)?f?
STRING : \"{LS}*\"
CHAR : '{LC}?'
BOOLEAN : (true|false)

VAR_INT: int
VAR_FLOAT: float
VAR_DOUBLE: double
VAR_STRING: string
VAR_CHAR: char
VAR_BOOLEAN: boolean
VAR_VOID: void

CONST : const
REF: ref
IF : if
ELSE : else
ELSEIF : elseif
WHILE : while
DO: do
FOR : for
WHEN : when
IS : is
RETURN : return
DEFAULT : default
PRINT : print
INPUT : input

AND : (and|&&)
OR : (or|\|\|)
NOT : (not|!)

GREATER: >
SMALLER: <
GREATER_EQ: >=
SMALLER_EQ: <=
EQ: ==
DIF: !=

INCREMENT_ASSIGNMENT: \+=
DECREMENT_ASSIGNMENT: \-=
MULTIPLICATION_ASSIGNMENT: \*=
DIVSION_ASSIGNMENT: /=
MOD_ASSIGNMENT: %=
BIT_OR_ASSIGNMENT: \|=
BIT_XOR_ASSIGNMENT: \^=
BIT_AND_ASSIGNMENT: &=
BIT_LS_ASSIGNMENT: <<=
BIT_RS_ASSIGNMENT: >>=

INCREMENT: \+\+
DECREMENT: \-\-
PLUS: \+
MINUS: \-
MULT: \*
DIV: /
MOD: %
ASSIGNMENT: =

BIT_AND: &
BIT_OR: \|
BIT_XOR: \^
BIT_NOT: ~
BIT_LS: <<
BIT_RS: >>

LSP : \(
RSP : \)
LSB : \[ 
RSB : \]
LSC : \{
RSC : \}
DOT : \.
END : ;
COLON : :
COMMA : ,

ID : [a-zA-Z_$][a-zA-Z_0-9$]*

: {CMT}
: [\s\t\n\r ]*


#NonTerminals
// Global scope
<global>
<global_stmt>

// Expressions
<exp>
<exp1>
<exp2>
<exp3>
<exp4>
<exp5>
<exp6>
<exp7>
<exp8>
<exp9>
<exp10>
<exp11>
<exp12>
<exp13>

<int_literal>
<literal>
<left_value>
<right_value>

// Operators
<op_comp>
<op_unary_right_mutable>
<op_unary_left>
<op_unary_left_mutable>
<op_bitwise>
<op_arit_low>
<op_arit_high>
<op_assignment>

// Declaration and assignment
<type>
<type_primitive>
<modifier_list>

// Statements and structure
<statement_list>
<statement>

<if_stmt>
<while_stmt>
<do_while_stmt>

<for_stmt>
<for_left>

<when_stmt>

<declaration_stmt>
<declaration_stmt_id_list>

<assignment_stmt>
<return_stmt>

<if>
<else_if>
<else_if_list>
<else>

<is_list>
<is>
<default>

// Scopes and functions
<scope>
<function>
<function_prototype>
<function_prototype_argument_list>

<function_call>
<function_call_argument_list>
#Grammar
// ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Global scope

<global> ::= <global_stmt> <global> | <global_stmt>;
<global_stmt> ::= <declaration_stmt> END | <assignment_stmt> END | <function>;

// ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Expressions

<literal> ::= <int_literal> #101 | FLOAT #102 | STRING #103 | CHAR #104 | BOOLEAN #105;
<int_literal> ::= INT_DEC | INT_HEX | INT_BIN;

<left_value> ::= ID #106 | ID #107 LSB <exp> #108 RSB;
<right_value> ::= <literal> | <function_call> | LSP <exp> RSP;

<exp>   ::= <exp> OR #122 <exp1> #110 | <exp1>;
<exp1>  ::= <exp1> AND #123 <exp2> #111 | <exp2>;
<exp2>  ::= <exp2> BIT_OR #124 <exp3> #112 | <exp3>; 
<exp3>  ::= <exp3> BIT_XOR #125 <exp4> #113 | <exp4>;
<exp4>  ::= <exp4> BIT_AND #126 <exp5> #114 | <exp5>;
<exp5>  ::= <exp5> <op_comp> <exp6> #115 | <exp6>;
<exp6>  ::= <exp6> <op_bitwise> <exp7> #116 | <exp7>;
<exp7>  ::= <exp7> <op_arit_low> <exp8> #117 | <exp8>;
<exp8>  ::= <exp8> <op_arit_high> <exp9> #118 | <exp9>;
<exp9>  ::= <left_value> #119 <op_unary_right_mutable> #121 | <exp10>;
<exp10> ::= <op_unary_left> <left_value> #119 #121 | <exp11>;
<exp11> ::= <op_unary_left_mutable> <left_value> #119 #121 | <exp12>;
<exp12> ::= <op_unary_left> <right_value> #120 | <exp13>;
<exp13> ::= <left_value> #109 | <right_value>;

// Operators
<op_comp> ::= GREATER #127 | SMALLER #128 | GREATER_EQ #129 | SMALLER_EQ #130 | EQ #131 | DIF #132;
<op_bitwise> ::= BIT_LS #133 | BIT_RS #134;
<op_arit_low> ::= PLUS #135 | MINUS #136;
<op_arit_high> ::= MULT #137 | DIV #138 | MOD #139;
<op_unary_right_mutable> ::= INCREMENT #140 | DECREMENT #141;
<op_unary_left> ::= MINUS #142 | BIT_NOT #143 | NOT #144;
<op_unary_left_mutable> ::= INCREMENT #145 | DECREMENT #146;
<op_assignment> ::= ASSIGNMENT #147 | INCREMENT_ASSIGNMENT #148 | DECREMENT_ASSIGNMENT #149 | MULTIPLICATION_ASSIGNMENT #150 | DIVSION_ASSIGNMENT #151 | MOD_ASSIGNMENT #152 | BIT_OR_ASSIGNMENT #153 | BIT_XOR_ASSIGNMENT #154 | BIT_AND_ASSIGNMENT #155 | BIT_LS_ASSIGNMENT #156 | BIT_RS_ASSIGNMENT #158;

// ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Declaration and assignment

<type> ::= <modifier_list> <type_primitive> | <modifier_list> <type_primitive> LSB <int_literal> #208 RSB;
<type_primitive> ::= VAR_INT #201 | VAR_FLOAT #202 | VAR_DOUBLE #203 | VAR_STRING #204 | VAR_CHAR #205 | VAR_BOOLEAN #206 | VAR_VOID #207;
<modifier_list> ::= CONST #200 | î;

// ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Statements and structure

<statement_list> ::= <statement> <statement_list> | <statement>;
<statement> ::= <if_stmt> | <while_stmt> | <do_while_stmt> END | <for_stmt> | <assignment_stmt> END | <declaration_stmt> END | <return_stmt> END | <when_stmt> | <exp> #503 END;

<if_stmt> ::= <if> #319 | <if> <else> #319 | <if> <else_if_list> #319 | <if> <else_if_list> <else> #319;
<while_stmt> ::= WHILE #307 LSP <exp> #308 #302 RSP #158 <scope> #309 #159;
<do_while_stmt> ::= DO #310 #158 <scope> #159 WHILE LSP <exp> #311 #303 RSP;

<for_stmt> ::= FOR #158 LSP <for_left> END #312 <exp> #313 #304 END #500 <exp> #501 RSP <scope> #502 #314 #159;
<for_left> ::= <exp> | <declaration_stmt> | <assignment_stmt>;

<when_stmt> ::= WHEN LSP <exp> #323 #305 RSP LSC <is_list> RSC #503 #321;

<declaration_stmt> ::= <type> <declaration_stmt_id_list> #210;
<declaration_stmt_id_list> ::= ID #209 | ID #209 COMMA <declaration_stmt_id_list> | ID #209 ASSIGNMENT #147 <exp> #211 | ID #209 ASSIGNMENT #147 <exp> #211 COMMA <declaration_stmt_id_list>;

<assignment_stmt> ::= <left_value> #212 <op_assignment> <exp> #211 #213;
<return_stmt> ::= RETURN <exp> #407;

// Blocks
<if> ::= IF LSP <exp> #315 #300 RSP #158 <scope> #316 #159;
<else_if_list> ::= <else_if> <else_if_list> | <else_if>;
<else_if> ::= ELSEIF LSP <exp> #317 #301 RSP #158 <scope> #318 #159;
<else> ::= ELSE #158 <scope> #159;

<is_list> ::= <is> | <is> <is_list> | <default>;
<is> ::= IS LSP #324 <exp> #320 #306 RSP #158 <scope> #322 #159;
<default> ::= DEFAULT #158 <scope> #159;

// ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Scopes and functions

<scope> ::= LSC RSC | LSC <statement_list> RSC;
<function> ::= <function_prototype> #406 #402 <scope> #159;
<function_prototype> ::= <type> ID #400 LSP RSP | <type> ID #400 LSP <function_prototype_argument_list> RSP;
<function_prototype_argument_list> ::= <type> ID #401 COMMA <function_prototype_argument_list> | <type> ID #401;

<function_call> ::= ID #403 LSP RSP #405 | ID #403 LSP <function_call_argument_list> RSP #405 | PRINT LSP <exp> RSP #408 | INPUT LSP RSP #409;
<function_call_argument_list> ::= <exp> #404 COMMA <function_call_argument_list> | <exp> #404;
